;; Write all scheme.org DNS records into the file "dns.zone".
;;
;; File format: <https://en.wikipedia.org/wiki/Zone_file>
;;
;; This should run in any R7RS-small implementation.

(import (scheme base) (scheme file) (scheme read) (scheme write))

(define scheme-dot-org-ipv4 "8.9.4.141")
(define scheme-dot-org-ipv6 "2001:19f0:5:6000:5400:02ff:fe07:9aa6")
(define time-to-live "10800")

(define (echo . strings) (for-each write-string strings) (newline))

(define (get-list property alist) (cdr (or (assoc property alist) '(#f))))

(define (get-string? property alist)
  (let ((pair (assoc property alist))) (and pair (cadr pair))))

(define (get-string property alist)
  (or (get-string? property alist) (error "Not defined:" property alist)))

(define (string-last s)
  (let ((n (string-length s))) (and (> n 0) (string-ref s (- n 1)))))

(define projects-scm (call-with-input-file "projects.scm" read))
(define project-groups (get-list 'projects projects-scm))

(define (echo-dns-record subdomain rec)
  (let ((name (get-string? 'name (cdr rec)))
        (type (get-string 'type (cdr rec)))
        (data (get-string 'data (cdr rec))))
    (set! name (if name (string-append name "." subdomain) subdomain))
    (cond ((member type '("A" "AAAA")))
          ((member type '("CNAME" "MX"))
           (unless (eqv? #\. (string-last data))
             (error "Data must end with a dot:" data)))
          (else (error "Unupported DNS record type:" type)))
    (echo name " " time-to-live " IN " type " " data)))

(define (echo-zone-file)
  (echo "; DNS zone file for scheme.org generated by dns.scm")
  (echo)
  (echo-dns-record "@" `(rec (type "A")    (data ,scheme-dot-org-ipv4)))
  (echo-dns-record "@" `(rec (type "AAAA") (data ,scheme-dot-org-ipv6)))
  (for-each
   (lambda (group)
     (for-each
      (lambda (project)
        (let ((project-id (get-string 'project-id project)))
          (let ((dns (get-list 'dns project)))
            (unless (null? dns)
              (echo)
              (for-each (lambda (rec) (echo-dns-record project-id rec))
                        dns)))))
      (cdr group)))
   project-groups))

(define (main)
  (echo "Writing dns.zone")
  (with-output-to-file "dns.zone" echo-zone-file)
  0)

(main)
